# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgname=etcd
pkgver=3.2.13
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/coreos/etcd'
license=('Apache')
makedepends=('go' 'glide')
backup=('etc/etcd/etcd.conf.yml')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/contrib/systemd/${pkgname}.service"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/${pkgname}.conf.yml.sample")
sha256sums=('2d3da8e5fff8cbf70bc0fe2ea193ec74a1c4acdb42504bd2f1e391d163b4cdf6'
            '400b28d6e643d1f60d89813e7acdf6b80c9bee42711e650a058101e7746173e9'
            '6a70e5dc62bef30600a6b13f7baaa2a173299a3648b6e8c1adecd095f3a5d6c1')

prepare() {
  # setup local gopath
  mkdir -p $srcdir/src/github.com/coreos
  ln -s $srcdir/${pkgname}-${pkgver} $srcdir/src/github.com/coreos/$pkgname

  cd $srcdir/src/github.com/coreos/$pkgname

  GOPATH="$srcdir" glide install --strip-vendor
}

build() {
  cd $srcdir/src/github.com/coreos/$pkgname
  GOPATH="$srcdir" ./build
}

package() {
  cd $srcdir
  install -Dm644 ${pkgname}.conf.yml.sample $pkgdir/etc/etcd/$pkgname.conf.yml
  install -Dm644 ${pkgname}.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm755 ${pkgname}-${pkgver}/bin/etcd $pkgdir/usr/bin/etcd
  install -Dm755 ${pkgname}-${pkgver}/bin/etcdctl $pkgdir/usr/bin/etcdctl
  install -Dm644 ${pkgname}-${pkgver}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -dm755 $pkgdir/usr/share/doc/$pkgname
  install -m644 ${pkgname}-${pkgver}/Documentation/*.md -t $pkgdir/usr/share/doc/$pkgname
}

# vim:set sw=2 sts=2 ft=sh et:
