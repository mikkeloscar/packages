# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgname=etcd
pkgver=3.4.6
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/coreos/etcd'
license=('Apache')
makedepends=('go' 'glide')
backup=('etc/etcd/etcd.conf.yml')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/contrib/systemd/${pkgname}.service"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/${pkgname}.conf.yml.sample")
sha256sums=('e9ebd003f5545a05017a8dbdde236d6c9d25f98ee35f8ba237e57b75330664f9'
            'e107490b80d96494b9276c7478e2e4d368ef9254b50a7996ae9f3456ccc68e81'
            'd91862eedcd2d98523308dba0e4da4ce7a41554b74f52c62a9a69f175753da37')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
  rm -rf vendor
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./build
}

package() {
  cd $srcdir
  install -Dm644 ${pkgname}.conf.yml.sample $pkgdir/etc/etcd/$pkgname.conf.yml
  install -Dm644 ${pkgname}.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm755 ${pkgname}-${pkgver}/bin/etcd $pkgdir/usr/bin/etcd
  install -Dm755 ${pkgname}-${pkgver}/bin/etcdctl $pkgdir/usr/bin/etcdctl
  install -Dm644 ${pkgname}-${pkgver}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set sw=2 sts=2 ft=sh et:
