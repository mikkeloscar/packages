# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgname=etcd
pkgver=3.5.6
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/coreos/etcd'
license=('Apache')
makedepends=('go')
backup=('etc/etcd/etcd.conf.yml')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/contrib/systemd/${pkgname}.service"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/${pkgname}.conf.yml.sample")
sha256sums=('0ef11c0cdc3cc693756a636d97d280ea63e222a7be38159fad0c8032ced0da42'
            '6b2921568bd96f5bb48ba61d2b9673f42e49eb90fda35a7003fbac469a226a13'
            '4b61e989a358a76df6b7ff68d5a026a042a671ad8d8d67a0244a58e80b61082a')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
  rm -rf vendor
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./build.sh
}

package() {
  cd $srcdir
  install -Dm644 ${pkgname}.conf.yml.sample $pkgdir/etc/etcd/$pkgname.conf.yml
  install -Dm644 ${pkgname}.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm755 ${pkgname}-${pkgver}/bin/etcd $pkgdir/usr/bin/etcd
  install -Dm755 ${pkgname}-${pkgver}/bin/etcdctl $pkgdir/usr/bin/etcdctl
  install -Dm644 ${pkgname}-${pkgver}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set sw=2 sts=2 ft=sh et:
