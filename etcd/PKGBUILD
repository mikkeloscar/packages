# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgname=etcd
pkgver=3.5.0
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/coreos/etcd'
license=('Apache')
makedepends=('go')
backup=('etc/etcd/etcd.conf.yml')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/contrib/systemd/${pkgname}.service"
        "https://github.com/coreos/${pkgname}/raw/v${pkgver}/${pkgname}.conf.yml.sample")
sha256sums=('f30f68c52a7547af08be7d166884c94885ea8a593c1c1e814c89b24148ee1921'
            '6b2921568bd96f5bb48ba61d2b9673f42e49eb90fda35a7003fbac469a226a13'
            '1a532bf85dbb94fb370e1b69e4384e8bf5cd93b3cea778e9f9fc9dfe15009328')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
  rm -rf vendor
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./build.sh
}

package() {
  cd $srcdir
  install -Dm644 ${pkgname}.conf.yml.sample $pkgdir/etc/etcd/$pkgname.conf.yml
  install -Dm644 ${pkgname}.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm755 ${pkgname}-${pkgver}/bin/etcd $pkgdir/usr/bin/etcd
  install -Dm755 ${pkgname}-${pkgver}/bin/etcdctl $pkgdir/usr/bin/etcdctl
  install -Dm644 ${pkgname}-${pkgver}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set sw=2 sts=2 ft=sh et:
