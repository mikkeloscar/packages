# Maintainer: Mateusz Paluszkiewicz <aifam96 at gmail dot com>
# Contributor: Swift Geek <swiftgeek plus aur at gmail dot com>
# Contributor: Jerome Leclanche <jerome at leclan dot ch>
# Contributor: Andrea Scarpino <andrea at archlinux dot org>
# Contributor: Pier Luigi Fiorini <pierluigi dot fiorini at gmail dot com>
# Contributor: Gustavo <sl1pkn07 at gmail dot com>
# Contributor: David Manouchehri <david@davidmanouchehri.com>

_pkgname=qt5-wayland
pkgname=${_pkgname}-git
pkgver=5.5.0.alpha1.r4.g21a7983
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtWayland) - Version for Developers!"
arch=("i686" "x86_64")
url="https://qt.gitorious.org/qt/qtwayland/"
license=("GPL3" "LGPL")
depends=("qt5-base" "qt5-declarative" "libxcomposite" "wayland" "libdrm" "libxkbcommon" "mesa")
optdepends=("weston")
makedepends=("git")
provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")
options=("staticlibs" "libtool")
source=("$_pkgname::git+https://gitorious.org/qt/qtwayland.git#branch=dev")
sha256sums=("SKIP")

# Definition with userside version of Qt5
_qt5_userversion=`pkg-config --modversion Qt5Core`

pkgver() {
  cd "${_pkgname}"
  #pkg ver withtout 'v' prefix
  git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${_pkgname}"

  mkdir build
  cd build

  export QT_SELECT=5
  msg "Preparing library... (QMAKE_LIBS_PRIVATE is NEEDED now)"
  qmake CONFIG+=wayland-compositor QMAKE_LIBS_PRIVATE+=-lXrender ..

  msg "Build library..."
  make -j`nproc`
}

package() {
  cd "${_pkgname}"
  cd build

  msg "Packing library..."
  make INSTALL_ROOT="${pkgdir}" install

  # Create custom directories
  mkdir -p ${pkgdir}/usr/share/licenses/qt5-wayland/

  # Install generated private headers
  #cp ./include/QtCompositor/${_qt5_userversion}/QtCompositor/private/{qwayland-server-*,*protocol*}.h ${pkgdir}/usr/include/qt/QtCompositor/${_qt5_userversion}/QtCompositor/private/

  # TODO: Needed?
  cp ./include/QtCompositor/${_qt5_userversion}/QtCompositor/private/*.h ${pkgdir}/usr/include/qt/QtCompositor/${_qt5_userversion}/QtCompositor/private/

  # Back to main directory
  cd ../

  # Copy custom files
  cp ./LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/qt5-wayland/
}
