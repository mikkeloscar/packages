# Maintainer: Mikkel Oscar Lyderik Larsen <m at moscar dot net>

_github_org=kubernetes-incubator
pkgname=cri-o
pkgver=1.9.0
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="Open Container Initiative-based implementation of Kubernetes Container
Runtime Interface"
arch=('any')
url="https://github.com/${_github_org}/$pkgname"
license=('Apache 2')
makedepends=("go")
depends=("runc" "ostree" "btrfs-progs")
source=("https://github.com/${_github_org}/$pkgname/archive/v${_pkgver}.tar.gz"
        "crio.service")
sha256sums=('82c4d6a892e32bfb7f0b2bf0eab0fc6b793734c24f11f3fb936aff042501a036'
            '2eda47d507a042721a66dc52503d41e8c107f1f313f3b61c5227f6c91c17c793')

prepare() {
  # setup local gopath
  mkdir -p $srcdir/src/github.com/${_github_org}
  ln -s $srcdir/$pkgname-${_pkgver} $srcdir/src/github.com/${_github_org}/$pkgname

  cd $srcdir/src/github.com/${_github_org}/$pkgname

  GOPATH="$srcdir" make .install.md2man
}

build() {
  cd $srcdir/src/github.com/${_github_org}/$pkgname
  GOPATH="$srcdir" PATH=$PATH:$srcdir/bin make -j1
}

package() {
  cd "$srcdir/$pkgname-${_pkgver}"

  # generate default config
  sed -i 's#/usr/local/libexec#/usr/libexec#' crio.conf

  make DESTDIR="$pkgdir" PREFIX=$pkgdir/usr install install.config install.completions install.systemd
  install -Dm644 "test/policy.json" $pkgdir/etc/containers/policy.json
  # install -Dm644 ../crio.service $pkgdir/usr/lib/systemd/system/crio.service

  # install manpages
  install -d $pkgdir/usr/share/man/man5/
  install -d $pkgdir/usr/share/man/man8/
  install -pm644 docs/crio.conf.5 $pkgdir/usr/share/man/man5
  install -pm644 docs/crio.8 $pkgdir/usr/share/man/man8
}

# vim:set ts=2 sw=2 ft=sh et:
