# Maintainer: Quey-Liang Kao<s101062801@m101.nthu.edu.tw>
# Contributor: Iwan Timmer <irtimmer@gmail.com>

_github_org=opencontainers
_pkgname=runc
pkgname=${_pkgname}-git
pkgver=v1.0.0.rc4.r217.ga618ab5a
pkgrel=1
pkgdesc="Container CLI tools"
depends=('glibc')
makedepends=('git' 'godep' 'go')
arch=('x86_64' 'i686')
url="http://runc.io/"
provides=('runc')
conflicts=('runc')
options=('!strip')
license=("APACHE")
source=("git+https://github.com/${_github_org}/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
    cd $srcdir/$_pkgname
    ( set -o pipefail
      git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
  # setup local gopath
  mkdir -p $srcdir/src/github.com/${_github_org}
  ln -s $srcdir/$_pkgname $srcdir/src/github.com/${_github_org}/$_pkgname
}

build() {
  cd $srcdir/src/github.com/${_github_org}/$_pkgname
  GOPATH="$srcdir" make
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 runc $pkgdir/usr/bin/runc
}
