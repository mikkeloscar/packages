# Maintainer: Mikkel Oscar Lyderik <mikkeloscar at gmail dot com>

_pkgname=drone-cli
pkgname=$_pkgname-git
_goname=drone
pkgver=r41.38d6ebf
pkgrel=1
pkgdesc="drone.io command line utility"
arch=("i686" "x86_64")
url="http://readme.drone.io/cli/"
license=('Apache 2.0')
makedepends=("go")
depends=("docker")
provides=("drone-cli")
conflicts=("drone-cli")
source=("git+https://github.com/drone/drone-cli.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  # setup local gopath
  mkdir -p $srcdir/src/github.com/drone
  ln -s $srcdir/$_pkgname $srcdir/src/github.com/drone/$_pkgname

  cd $srcdir/src/github.com/drone/$_pkgname

  GOPATH="$srcdir" go get -v -d ./drone/...
}

build() {
  cd $srcdir/src/github.com/drone/$_pkgname
  GOPATH="$srcdir" make bin/drone
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 bin/drone $pkgdir/usr/bin/drone
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$_pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
