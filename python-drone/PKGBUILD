# Maintainer: Mikkel Oscar Lyderik <mikkeloscar at gmail dot com>

_realname=drone
pkgbase=python-drone
pkgname=('python-drone' 'python2-drone')
pkgver=0.3.0
pkgrel=1
pkgdesc='Drone API Client and plugin utilities for Python'
url='https://github.com/drone/drone-python'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/d/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('82498032495dbd84faa34443737b6eefb684edf593402ecd8bc3d6fe6a91165b')

prepare() {
  cp -a ${_realname}-${pkgver}{,-python2}
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_realname}-${pkgver}-python2"
  python2 setup.py build
}

package_python-drone() {
  pkgdesc='Drone API Client and plugin utilities for Python 3'
  depends=('python-future')

  cd "${srcdir}/${_realname}-${pkgver}"
  python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
}

package_python2-drone() {
  pkgdesc='Drone API Client and plugin utilities for Python 2'
  depends=('python2-future')

  cd "${srcdir}/${_realname}-${pkgver}-python2"
  python2 setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build

  # rename for python2
  mv $pkgdir/usr/bin/create-drone-py-plugin $pkgdir/usr/bin/create-drone-py2-plugin
}

# vim:set ts=2 sw=2 et:
