# Maintainer: Mikkel Oscar Lyderik <mikkeloscar at gmail dot com>
# Contributer: Sebastian Wilzbach <sebi at wilzbach dot me>

pkgname=drone
pkgver=0.4.2
pkgrel=1
pkgdesc="Drone is a Continuous Integration platform built on Docker, written in Go"
arch=('i686' 'x86_64')
url="http://github.com/drone/drone"
license=('Apache 2')
makedepends=('git' 'go' 'sassc')
depends=('docker')
provides=('drone')
conflicts=('drone')
backup=('etc/drone/dronerc')
install='drone.install'
source=("https://github.com/drone/drone/archive/v${pkgver}.tar.gz"
        'drone.service'
        'dronerc')
md5sums=('b402e179d39b66fa057be121d21b7755'
         'ae07870810f2a48ff9ea7af29adf4e37'
         'f9cf2b16b8b47f82439192ef5cbcf7fc')

prepare() {
  # setup local gopath
  mkdir -p $srcdir/src/github.com/drone
  ln -s $srcdir/$pkgname-$pkgver $srcdir/src/github.com/drone/$pkgname

  cd $srcdir/src/github.com/drone/$pkgname

  GOPATH="$srcdir" PATH=$PATH:$GOPATH/bin make deps
  GOPATH="$srcdir" PATH=$PATH:$GOPATH/bin make gen
}

build() {
  cd $srcdir/src/github.com/drone/$pkgname
  GOPATH="$srcdir" PATH=$PATH:$GOPATH/bin make build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # server
  install -Dm755 drone/drone $pkgdir/usr/bin/droned

  # license
  install -Dm644 LICENSE $pkgdir/usr/share/$pkgname
  install -Dm644 README.md $pkgdir/usr/share/$pkgname

  # service
  install -Dm644 $srcdir/drone.service $pkgdir/usr/lib/systemd/system/drone.service

  # config
  install -Dm644 $srcdir/dronerc $pkgdir/etc/drone/dronerc

  # db path
  install -dm700 $pkgdir/var/lib/drone
  chown -R 633:633 $pkgdir/var/lib/drone
}

# vim:set ts=2 sw=2 ft=sh et:
